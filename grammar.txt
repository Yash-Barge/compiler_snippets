<program>===><otherFunctions> <mainFunction>
<mainFunction>===>TK_MAIN <stmts> TK_END
<otherFunctions>===><function><otherFunctions> | EPS
<function>===>TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END
<input_par>===>TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par>===>TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR | EPS
<parameter_list>===><dataType> TK_ID <remaining_list>
<dataType>===><primitiveDatatype> | <constructedDatatype>
<primitiveDatatype>===>TK_INT | TK_REAL
<constructedDatatype>===>TK_RECORD TK_RUID | TK_UNION TK_RUID | TK_RUID
<remaining_list>===>TK_COMMA <parameter_list> | EPS
<stmts>===><typeDefinitions> <declarations> <otherStmts> <returnStmt>
<typeDefinitions>===><typeDefinition> <typeDefinitions> | <definetypestmt> <typeDefinitions> | EPS
<typeDefinition>===>TK_RECORD TK_RUID <fieldDefinitions> TK_ENDRECORD | TK_UNION TK_RUID <fieldDefinitions> TK_ENDUNION
<fieldDefinitions>===><fieldDefinition> <fieldDefinition> <moreFields>
<fieldDefinition>===>TK_TYPE <dataType> TK_COLON TK_FIELDID TK_SEM
<moreFields>===><fieldDefinition> <moreFields> | EPS
<declarations>===><declaration> <declarations> | EPS
<declaration>===>TK_TYPE <dataType> TK_COLON TK_ID <global_or_not> TK_SEM
<global_or_not>===>TK_COLON TK_GLOBAL | EPS
<otherStmts>===><stmt><otherStmts> | EPS
<stmt>===><assignmentStmt> | <iterativeStmt> | <conditionalStmt> | <ioStmt> | <funCallStmt>
<assignmentStmt>===><singleOrRecId> TK_ASSIGNOP <arithmeticExpression2> TK_SEM
<singleOrRecId>===>TK_ID <singleOrRecId_>
<singleOrRecId_>===>TK_DOT  <recursiveFieldIDs> | EPS
<recursiveFieldIDs>===>TK_FIELDID <recursiveFieldIDs_>
<recursiveFieldIDs_>===>TK_DOT <recursiveFieldIDs> | EPS
<funCallStmt>===><outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM
<outputParameters>===>TK_SQL <idList> TK_SQR TK_ASSIGNOP | EPS
<inputParameters>===>TK_SQL <idList> TK_SQR
<iterativeStmt>===>TK_WHILE TK_OP <booleanExpression> TK_CL <stmt><otherStmts> TK_ENDWHILE
<conditionalStmt>===>TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt><otherStmts> <conditionalStmt_>
<conditionalStmt_>===>TK_ELSE <stmt><otherStmts> TK_ENDIF | TK_ENDIF
<ioStmt>===>TK_READ TK_OP <singleOrRecId> TK_CL TK_SEM | TK_WRITE TK_OP <var> TK_CL TK_SEM
<arithmeticExpression2>===><var> | TK_OP <arithmeticExpression0> TK_CL
<arithmeticExpression1>===><arithmeticExpression2>  <arithmeticExpression1_>
<arithmeticExpression1_>===>TK_DIV  <arithmeticExpression1_> | TK_MUL <arithmeticExpression1_> | EPS
<arithmeticExpression0>===><arithmeticExpression1> <arithmeticExpression0_>
<arithmeticExpression0_>===>TK_MINUS <arithmeticExpression0_> | TK_PLUS <arithmeticExpression0_> | EPS
<booleanExpression>===>TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL | <var> <relationalOp> <var> | TK_NOT TK_OP <booleanExpression> TK_CL
<var>===>TK_NUM | TK_RNUM | <singleOrRecId>
<logicalOp>===>TK_AND | TK_OR
<relationalOp>===>TK_LT | TK_LE | TK_EQ |TK_GT | TK_GE | TK_NE
<returnStmt>===>TK_RETURN <optionalReturn> TK_SEM
<optionalReturn>===>TK_SQL <idList> TK_SQR | EPS
<idList>===>TK_ID <more_ids>
<more_ids>===>TK_COMMA <idList> | EPS
<definetypestmt>===>TK_DEFINETYPE <A> TK_RUID TK_AS TK_RUID
<A>===>TK_RECORD | TK_UNION
